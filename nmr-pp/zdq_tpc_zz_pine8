; zdq_tpc_zz
; 24 May 2022
; ZZ TPC Experiment for effective H = J*DQ
; Hamiltonian engineered by Pine8 Sequence
; Andrew Stasiuk

#include <Avance.incl>

; define the delays for Ken16
; p1 should be length of a 90 deg pulse (~ 1 us)

; cnst1 should be greater than or equal to 1
define delay tau1
"tau1 = d2 * cnst1 - p1"
define delay tau2
"tau2 = 2 * d2 * cnst1 - p1"

; This delay is the length of the entire Ham Eng Cycle
; It is given by 12*d2, d2 should be  >= 2.5 us.
define delay cycle
"cycle=12*d2*cnst1"

; First measurement length.
; Measurements occur at intervals of cycle*(x+l1), x=0,...,td1
"l1=0"

; start the pulse program
1   ze

; state-reset delay
2   d1
    ; set the power level of a 90 deg pulse to be pl1 on channel 2 (assuming pulse p1)
    cycle pl1:f2

; Ken16 Hamiltonian engineering
3
    tau1
    (p1 ph10^):f2
    tau2
    (p1 ph10^):f2
    tau1
    (p1 ph10^):f2
    tau2
    (p1 ph10^):f2
    tau1
    (p1 ph10^):f2
    tau2
    (p1 ph10^):f2
    tau1
    (p1 ph10^):f2
    tau2
    (p1 ph10^):f2


    lo to 3 times l1

    1m

; Change observable from X to Z
4   (p1 ph30):f2

    go=2 ph31

    1m wr #0 if #0

    ; increase evolution time by 1 cycle length, and re-scan
    1m iu1
    lo to 2 times td1

    exit

; Ken16 phase programs
ph10 = 0 0 2 2 2 2 0 0

; final pulse phase program (maybe will help decouple?)
ph30 = 1 3 3 1
ph31 = 1 3 3 1
