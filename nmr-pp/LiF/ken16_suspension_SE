; Ken16 dipolar testing
; suspension with disorder

; cnst1 multiplies disZ, cnst2 multiplies Dz
; H_eff = (1/3)*cnst1*sum(h_i Z_i) + cnst2 * Dz

define delay tau1
"tau1 = d3*(1+cnst1+2*cnst2)-p1/2"
define delay tau2
"tau2 = d3*(1-cnst2)-p1"
define delay tau3
"tau3 = 2*d3*(1-cnst2)-p1"

define delay tau1p
"tau1p = d3*(1-cnst1+2*cnst2)-p1/2"

define delay cycle
"cycle=24*d3"

; First measurement length is 0. Ensure L[1]=0 as well.
"l1=0"

; start the pulse program
1   ze

; reset delay
2   d1 rpp0 rpp1 rpp2 rpp3 rpp5 rpp5 rpp6 rpp7
    cycle pl2:f2

; Kicking + Ken16
3
    tau1
    (p1 ph0^):f2
    tau2
    (p1 ph1^):f2
    tau3
    (p1 ph2^):f2
    tau2
    (p1 ph3^):f2
    tau1p

    tau1p
    (p1 ph4^):f2
    tau2
    (p1 ph5^):f2
    tau3
    (p1 ph6^):f2
    tau2
    (p1 ph7^):f2
    tau1

    lo to 3 times l1

    ; kill transients
    1m
    (p1 ph29):f2
    20u
    (p1 ph30):f2

    go=2 ph31
    1m wr #0 if #0

    ; increase evolution time by 1 cycle length, and re-scan
    1m iu1
    1m iu1
    lo to 2 times td1

    exit

ph0 = (360) 0 180
ph1 = (360) 90 270
ph2 = (360) 90 270
ph3 = (360) 0 180
ph4 = (360) 0 180
ph5 = (360) 90 270
ph6 = (360) 90 270
ph7 = (360) 0 180

ph29 = 0 2
ph30 = 1 3
ph31 = 0 2