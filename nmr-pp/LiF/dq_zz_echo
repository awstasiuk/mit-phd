; 19 January 2024
; Andrew Stasiuk
; ZZ echo Expt under Hamiltonian XX - YY
; Time reversal by X<->Y interchange.


define delay tau
"tau=d6-p1"
define delay tau2
"tau2=2*d6-p1"

define delay cycle
"cycle=12*d6"

"l10=0"

1   ze

2
    d1 rpp0 rpp1 rpp2 rpp3
    cycle pl2:f2

3
    (p1 ph0^):f2
    tau2
    (p1 ph1^):f2
    tau

    lo to 3 times l10

4
    (p1 ph2^):f2
    tau2
    (p1 ph3^):f2
    tau

    lo to 4 times l10

    1m

    (p1 ph30):f2

    go=2 ph31
    1m wr #0 if #0


    ; increase time evolution counters
    1m iu10
    1m iu10
    1m iu10
    1m iu10
    lo to 2 times td1

  exit

ph0 = (360) 0 180 180 0
ph1 = (360) 0 180 180 0

ph2 = (360) 90 270 270 90
ph3 = (360) 90 270 270 90

ph30 = 1 3
ph31 = 0 2
