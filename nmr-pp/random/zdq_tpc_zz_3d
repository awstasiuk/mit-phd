; zdq_tpc_zz_3d
; 18 Jan 2022
; ZZ TPC Experiment for effective H = h*Z+J*DQ
; Hamiltonian engineered by Ken16 Sequence
; run multiple times per time domain point to get error bars on each point
; Andrew Stasiuk

#include <Avance.incl>

; define the delays for Ken16
; p1 should be length of a 90 deg pulse (~ 1 us)
; cnst1 multiplies Z, cnst2 multiplies DQ strength
define delay tau1
"tau1 = d2*(1+cnst1)-p1/2"
define delay tau2
"tau2 = d2*(1-cnst2)-p1"
define delay tau3
"tau3 = 2*d2*(1+cnst2)-p1"

define delay tau1p
"tau1p = d2*(1-cnst1)-p1/2"
define delay tau2p
"tau2p = d2*(1-cnst2)-p1"
define delay tau3p
"tau3p = 2*d2*(1+cnst2)-p1"

; This delay is the length of the entire Ham Eng Cycle
; It is given by 24*d2, d2 should be ~ 5us.
define delay cycle
"cycle=24*d2"

; First measurement length.
; Measurements occur at intervals of cycle*(x+l1), x=0,...,td1
"l1=1"

; start the pulse program
1   ze

; state-reset delay
2   d1
    ; set the power level of a 90 deg pulse to be pl1 on channel 2 (assuming pulse p1)
    cycle pl1:f2

; Ken16 Hamiltonian engineering
3
    tau1
    (p1 ph10^):f2
    tau2
    (p1 ph10^):f2
    tau3
    (p1 ph10^):f2
    tau2p
    (p1 ph10^):f2
    tau1p

    tau1p
    (p1 ph10^):f2
    tau2
    (p1 ph10^):f2
    tau3p
    (p1 ph10^):f2
    tau2p
    (p1 ph10^):f2
    tau1

    tau1
    (p1 ph11^):f2
    tau2p
    (p1 ph11^):f2
    tau3p
    (p1 ph11^):f2
    tau2
    (p1 ph11^):f2
    tau1p

    tau1p
    (p1 ph11^):f2
    tau2p
    (p1 ph11^):f2
    tau3
    (p1 ph11^):f2
    tau2
    (p1 ph11^):f2
    tau1

    lo to 3 times l1

    1m

; Change observable from X to Z
4   (p1 ph30):f2

    go=2 ph31

    1m wr #0 if #0
    
    ; re-acquire data td2 times per a given evolution time
    lo to 2 times td2

    ; increase evolution time by 1 cycle length, and re-scan
    1m iu1
    lo to 2 times td1

    exit




; Ken16 phase programs
ph10 = 0 1 1 0
ph11 = 2 3 3 2

; final pulse phase program (maybe will help decouple?)
ph30 = 1 3 3 1
ph31 = 1 3 3 1